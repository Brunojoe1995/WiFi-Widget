plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.dagger.hilt.android'
    id 'kotlin-kapt'
}

android {
    def PACKAGE_NAME = 'com.w2sv.wifiwidget'

    namespace PACKAGE_NAME
    compileSdk rootProject.ext.compileSdk

    defaultConfig {
        applicationId PACKAGE_NAME
        minSdk rootProject.ext.minSdk
        targetSdk rootProject.ext.compileSdk

        versionCode 2
        versionName '1.0.1'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    signingConfigs {
        // create release signingConfig from keystore.properties file, if present
        release {
            rootProject.file("keystore.properties").with { file ->
                if (file.exists()) {
                    def keystoreProperties = new Properties()
                    keystoreProperties.load(new FileInputStream(file))

                    storeFile rootProject.file("keys.jks")
                    storePassword keystoreProperties["storePassword"]
                    keyAlias keystoreProperties["keyAlias"]
                    keyPassword keystoreProperties["keyPassword"]
                } else
                    println("WARNING: Failed to create release signing configuration; $file.path not present")
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false  // TODO: figure out why altering colors when set to true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.0'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    // androidx
    implementation libs.androidx.core
    implementation libs.androidx.appcompat
    implementation libs.androidx.lifecycle.runtime
    implementation libs.androidx.splashscreen
    // .compose
    implementation libs.androidx.compose.activity
    implementation libs.androidx.compose.viewmodel
    implementation platform('androidx.compose:compose-bom:2022.10.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material:material:1.4.0-alpha04'  // TODO: Remove as soon as BottomSheet available for material3

    // hilt
    implementation libs.google.hilt
    kapt libs.google.hilt.kapt

    // other
    implementation libs.typedpreferences
    implementation libs.androidutils
    implementation libs.slimber

//    testImplementation 'junit:junit:4.13.2'

//    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
//    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
//    androidTestImplementation platform('androidx.compose:compose-bom:2022.10.00')
//    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'

    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}